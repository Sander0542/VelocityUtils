plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'me.qoomon.git-versioning' version '6.4.3'
}

String packagePath = 'com.sanderjochems.minecraft'
group = packagePath
version = '0.0.0'

gitVersioning.apply {
    branch {
        pattern = 'main'
        versionFormat = '${version}-${commit.short}${dirty}'
    }
    branch {
        pattern = 'dev'
        versionFormat = '${version}-${branch.slug}-${commit.short}${dirty}'
    }
    tag {
        pattern = 'v(?<tagVersion>[0-9].*)'
        versionFormat = '${tagVersion}'
    }
}

sourceCompatibility = '1.11'
targetCompatibility = '1.11'

repositories {
    mavenCentral()
    maven {
        name = 'velocitypowered-repo'
        url = 'https://repo.velocitypowered.com/releases/'
    }
    maven {
        name = 'minecraft-libraries'
        url = 'https://libraries.minecraft.net/'
    }
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven'
    }
}

dependencies {
    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    implementation 'org.bstats:bstats-velocity:3.0.2'
    annotationProcessor 'com.velocitypowered:velocity-api:3.1.1'
}

shadowJar {
    dependencies {
        relocate('org.bstats', packagePath + '.dependencies.bstats')
    }
    archiveClassifier.set('')
}

import org.apache.tools.ant.filters.ReplaceTokens

task processSources(type: Sync) {
    from sourceSets.main.java.srcDirs
    inputs.property 'version', version
    filter ReplaceTokens, tokens: [version: version]
    into "$buildDir/src"
}

compileJava.source = processSources.outputs
