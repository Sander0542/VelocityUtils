plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.palantir.git-version' version '3.1.0'
}

group = 'com.sanderjochems.minecraft'
version = gitVersion()

repositories {
    mavenCentral()
    maven {
        name = 'velocitypowered-repo'
        url = 'https://repo.velocitypowered.com/releases/'
    }
    maven {
        name = 'minecraft-libraries'
        url = 'https://libraries.minecraft.net/'
    }
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven'
    }
}

dependencies {
    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    implementation 'org.bstats:bstats-velocity:3.0.2'
    annotationProcessor 'com.velocitypowered:velocity-api:3.1.1'
}

shadowJar {
    dependencies {
        relocate('org.bstats', group + '.dependencies.bstats')
    }
    archiveClassifier.set('')
}

task versionFile {
    def outputDir = layout.buildDirectory.dir("generated-sources/version").get().asFile
    def outputFile = new File(outputDir, "Version.java")

    inputs.property('version', version)

    doLast {
        outputFile.parentFile.mkdirs()
        outputFile.text = """
            package com.sanderjochems.minecraft.velocityutils;
            
            public class Version {
                public static final String VERSION = "${version}";
            }
        """.stripIndent()
    }
}

sourceSets.main.java.srcDirs += layout.buildDirectory.dir("generated-sources/version").get().asFile

compileJava.dependsOn versionFile