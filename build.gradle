plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'me.qoomon.git-versioning' version '6.4.4'
}

String packagePath = 'com.sanderjochems.minecraft'
group = packagePath
version = '0.0.0'

gitVersioning.apply {
    refs {
        branch('.+') {
            version = '${ref}-SNAPSHOT'
        }
        tag('v(?<version>.*)') {
            version = '${ref.version}'
        }
    }

    rev {
        version = '${commit}'
    }
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
    maven {
        name = 'velocitypowered-repo'
        url = 'https://repo.velocitypowered.com/releases/'
    }
    maven {
        name = 'minecraft-libraries'
        url = 'https://libraries.minecraft.net/'
    }
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven'
    }
}

dependencies {
    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    implementation 'org.bstats:bstats-velocity:3.2.0'
    annotationProcessor 'com.velocitypowered:velocity-api:3.1.1'
}

shadowJar {
    dependencies {
        relocate('org.bstats', packagePath + '.dependencies.bstats')
    }
    archiveClassifier.set('')
}

task processSources(type: Sync) {
    from sourceSets.main.java.srcDirs
    inputs.property 'version', version
    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [version: version]
    into layout.buildDirectory.dir("src")
}

compileJava.source = processSources.outputs
